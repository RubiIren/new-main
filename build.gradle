plugins {
    id 'java-library'
    id "io.qameta.allure" version "2.9.6"
}

repositories {
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

def allureVersion = "2.17.3",
    selenideVersion = "6.4.0",
    junitVersion = "5.8.2",
    slf4jVersion = "1.7.36",
    javafakerVersion = "1.0.2"

allure {
    report {
        version.set(allureVersion) //версия Allure Report (https://github.com/allure-framework/allure2)
    }
    adapter {
        aspectjWeaver.set(true) // обработка аннотации @Step
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion) //версия Allure JUnit5 (https://github.com/allure-framework/allure-java)
            }
        }
    }
}

dependencies {
    // Selenide
    testImplementation("com.codeborne:selenide:$selenideVersion")
    testRuntimeOnly("org.slf4j:slf4j-simple:$slf4jVersion") //пишет лог при запуске тестов

    // JUnit 5
    testImplementation("org.junit.jupiter:junit-jupiter:$junitVersion")

    //Faker
    testImplementation("com.github.javafaker:javafaker:$javafakerVersion")

    //Allure
    testImplementation("io.qameta.allure:allure-selenide:$allureVersion")

    testImplementation("org.aeonbits.owner:owner:1.0.12")

}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task demoqa_tests(type: Test) {
    useJUnitPlatform {
        includeTags "demoqa"
    }
}
